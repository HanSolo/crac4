import java.text.SimpleDateFormat

plugins {
    id 'java-library'
    id 'application'
    id 'net.nemerosa.versioning' version '3.0.0'
}

description = 'CRaC 4 demo'

Date buildTimeAndDate = new Date()
ext {
    buildDate = new SimpleDateFormat('yyyy-MM.dd').format(buildTimeAndDate)
    buildTime = new SimpleDateFormat('HH:mm:ss.SSSZ').format(buildTimeAndDate)
}

application {
    mainModule = 'eu.hansolo.crac4'
    mainClass  = 'eu.hansolo.crac4.Main'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation ("io.github.crac:org-crac:0.1.3")
}

compileJava {
    options.compilerArgs += ["-Aproject=${project.group}/${project.name}"]
}

jar {
    manifest {
        attributes(
                'Build-By'              : System.properties['user.name'],
                'Created-By'            : System.properties['java.version'] + " (" + System.properties['java.vendor'] + "  " + System.properties['java.vm.version'] + ")",
                'Build-Date'            : project.buildDate,
                'Build-Time'            : project.buildTime,
                'Build-Revision'        : versioning.info.commit,
                'Specification-Title'   : project.name,
                'Specification-Version' : project.version,
                'Implementation-Title'  : project.name,
                'Main-Class'            : application.mainClass.get()

        )
    }
}

if (JavaVersion.current().isJava8Compatible()){
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

task Main (type: JavaExec) {
    mainClass = "eu.hansolo.crac4.Main"
    classpath = sourceSets.main.runtimeClasspath
}